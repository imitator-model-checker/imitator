(************************************************************
 *                      IMITATOR MODEL                      
 *
 * FMS-1 from [LALSD14] (parameterized version)
 *
 * Description     : A flexible manufacturing system (FMS) [QCW05] produces blocks with a cylindrical painted pin from raw blocks and raw pegs. It consists of eleven devices, namely three conveyors, a mill, a lathe, a painting device, four robots, and an assembly machine. The devices are connected through six buffers, and the capacity of each buffer is one part. We modeled the FMS system in a constructive way such that three versions of models have been obtained, namely FMS-1 (the simplest one), FMS-2 (the medium one), and FMS-3 (the most complex one). Properties requiring that each buffer should not overflow or underflow and that output of each buffer should be within three time units after its input are verified. After verification, we found that three buffers have overflow and underflow problems. This model was extended to time in "Learning Assumptions for Compositional Verification of Timed Systems" [LALSD14]
 * Correctness     : Error state unreachable
 * Source          : M. H. Queiroz, J. E. R. Cury, and W. M. Wonham. Multitasking supervisory control of discrete-event systems. Discrete Event Dynamic Systems, 15(4), 2005.
 * Author          : M. H. Queiroz, J. E. R. Cury, and W. M. Wonham
 * Modeling        : Lin Shang-Wei
 * Input by        : Étienne André
 * License         : Creative Commons Attribution-ShareAlike 4.0 International (CC BY-SA 4.0)

 *
 * Created         : 2016/07/05
 * Last modified   : 2016/07/07
 *
 * IMITATOR version: 2.8-beta
 ************************************************************)

var

(* Clocks *)
	(* Conveyor1 *)
 	c_B1_in, c_I_B1_C1, xO_B1_R1,
 	
 	(* Mill *)
 	xI_B3_R1, xB3_in_M, xB3_out_M, xI_B3_M, xO_B3_R2, xO_B3_M,
 	
 	(* Robot1 *)
 	(*c_I_B1_C1, *)xB1_out, (*xO_B1_R1, *)xB3_in_R1, (*xI_B3_R1, *)(*xO_B3_M, *)
 	
 	(* Robot2 *)
 	(*xI_B3_M, *)xB3_out_R2, (*xO_B3_R2, *)xB5_in_R2, xI_B5_R2, xO_B5_A,
 	
 	(* AssemblyStation *)
 	(*xI_B5_R2, *)xB5_out_A, (*xO_B5_A, *)xproduct,
 	
 	(* Spec1 *)
	(*c_B1_in, xB1_out*)
	
	(* Spec3 *)
	(*xO_B3_M, xI_B3_M*)
		: clock;

(* Parameters *)
	p_conveyor, (* time from c_B1_in to I_B1_C1 *)
	p_deadline (* maximum time between B1_in and B1_out *)
		: parameter;



(************************************************************)
  automaton Conveyor1
(************************************************************)
synclabs: B1_in, I_B1_C1, O_B1_R1;

loc C0: while True wait {}
	when True sync B1_in do {c_B1_in' = 0} goto C1;

loc C1: while True wait {}
	when c_B1_in <= p_conveyor sync I_B1_C1 do {c_I_B1_C1' = 0} goto C2;

loc C2: while True wait {}
	when True sync O_B1_R1 do {xO_B1_R1' = 0} goto C0;

end (* Conveyor1 *)

(************************************************************)
  automaton Mill
(************************************************************)
synclabs: I_B3_R1, B3_in_M, B3_out_M, I_B3_M, O_B3_R2, O_B3_M;

loc M0: while True wait {}
	when True sync I_B3_R1 do {xI_B3_R1' = 0} goto M1;
	when True sync B3_in_M do {xB3_in_M' = 0} goto M3;

loc M1: while True wait {}
	when True sync B3_out_M do {xB3_out_M' = 0} goto M2;

loc M2: while True wait {}
	when True sync O_B3_M do {xO_B3_M' = 0} goto M0;

loc M3: while True wait {}
	when True sync I_B3_M do {xI_B3_M' = 0} goto M4;

loc M4: while True wait {}
	when True sync O_B3_R2 do {xO_B3_R2' = 0} goto M0;

end (* Mill *)

(************************************************************)
  automaton Robot1
(************************************************************)
synclabs: I_B1_C1, B1_out, O_B1_R1, B3_in_R1, I_B3_R1, O_B3_M;

loc R1_0: while True wait {}
	when True sync I_B1_C1 do {c_I_B1_C1' = 0} goto R1_1;

loc R1_1: while True wait {}
	when c_I_B1_C1 <= 1 sync B1_out do {xB1_out' = 0} goto R1_2;

loc R1_2: while True wait {}
	when True sync O_B1_R1 do {xO_B1_R1' = 0} goto R1_3;

loc R1_3: while True wait {}
	when True sync B3_in_R1 do {xB3_in_R1' = 0} goto R1_4;

loc R1_4: while True wait {}
	when True sync I_B3_R1 do {xI_B3_R1' = 0} goto R1_5;

loc R1_5: while True wait {}
	when True sync O_B3_M do {xO_B3_M' = 0} goto R1_0;

end (* Robot1 *)

(************************************************************)
  automaton Robot2
(************************************************************)
synclabs: I_B3_M, B3_out_R2, O_B3_R2, B5_in_R2, I_B5_R2, O_B5_A;

loc R2_0: while True wait {}
	when True sync I_B3_M do {xI_B3_M' = 0} goto R2_1;

loc R2_1: while True wait {}
	when True sync B3_out_R2 do {xB3_out_R2' = 0} goto R2_2;

loc R2_2: while True wait {}
	when True sync O_B3_R2 do {xO_B3_R2' = 0} goto R2_3;

loc R2_3: while True wait {}
	when True sync B5_in_R2 do {xB5_in_R2' = 0} goto R2_4;

loc R2_4: while True wait {}
	when True sync I_B5_R2 do {xI_B5_R2' = 0} goto R2_5;

loc R2_5: while True wait {}
	when True sync O_B5_A do {xO_B5_A' = 0} goto R2_0;

end (* Robot2 *)

(************************************************************)
  automaton AssemblyStation
(************************************************************)
synclabs: I_B5_R2, B5_out_A, O_B5_A, product;

loc A0: while True wait {}
	when True sync I_B5_R2 do {xI_B5_R2' = 0} goto A1;

loc A1: while True wait {}
	when True sync B5_out_A do {xB5_out_A' = 0} goto A2;

loc A2: while True wait {}
	when True sync O_B5_A do {xO_B5_A' = 0} goto A3;

loc A3: while True wait {}
	when True sync product do {xproduct' = 0} goto A0;

end (* AssemblyStation *)


(************************************************************)
  automaton Spec1
(************************************************************)
synclabs: B1_in, B1_out;

loc S0: while True wait {}
	when True sync B1_in do {c_B1_in' = 0} goto S1;
	when True sync B1_out do {xB1_out' = 0} goto Serror;

loc S1: while True wait {}
	when c_B1_in <= p_deadline sync B1_out do {xB1_out' = 0} goto S0;
	when c_B1_in > p_deadline sync B1_out do {xB1_out' = 0} goto Serror;
	when True sync B1_in do {c_B1_in' = 0} goto Serror;

loc Serror: while True wait {}

end (* Spec1 *)

(*

(************************************************************)
  automaton Spec3
(************************************************************)
synclabs: O_B3_M, I_B3_M;

loc S0: while True wait {}
	when True sync O_B3_M do {xO_B3_M' = 0} goto S1;
	when True sync I_B3_M do {xI_B3_M' = 0} goto Serror;

loc S1: while True wait {}
	when True sync O_B3_M do {xO_B3_M' = 0} goto Serror;
	when True sync I_B3_M do {xI_B3_M' = 0} goto S0;

loc Serror: while True wait {}

end (* Spec3 *)


(************************************************************)
  automaton Spec5
(************************************************************)
synclabs: B5_in_R2, B5_out_A;

loc S0: while True wait {}
	when True sync B5_in_R2 do {xB5_in_R2' = 0} goto S1;
	when True sync B5_out_A do {xB5_out_A' = 0} goto Serror;

loc S1: while True wait {}
	when True sync B5_in_R2 do {xB5_in_R2' = 0} goto Serror;
	when True sync B5_out_A do {xB5_out_A' = 0} goto S0;

loc Serror: while True wait {}

end (* Spec5 *)
*)

(************************************************************)
(* Initial state *)
(************************************************************)

init :=
	(*------------------------------------------------------------*)
	(* Initial location *)
	(*------------------------------------------------------------*)
	& loc[Conveyor1]		= C0
	& loc[Mill]				= M0
	& loc[Robot1]			= R1_0
	& loc[Robot2]			= R2_0
	& loc[AssemblyStation]	= A0
	& loc[Spec1]			= S0

	(*------------------------------------------------------------*)
	(* Initial clock constraints *)
	(*------------------------------------------------------------*)
	&
	(* Conveyor1 *)
 	c_B1_in = 0 &  c_I_B1_C1 = 0 &  xO_B1_R1 = 0 & 
 	
 	(* Mill *)
 	xI_B3_R1 = 0 &  xB3_in_M = 0 &  xB3_out_M = 0 &  xI_B3_M = 0 &  xO_B3_R2 = 0 &  xO_B3_M = 0 & 
 	
 	(* Robot1 *)
 	(*c_I_B1_C1 = 0 &  *)xB1_out = 0 &  (*xO_B1_R1 = 0 &  *)xB3_in_R1 = 0 &  (*xI_B3_R1 = 0 &  *)(*xO_B3_M = 0 &  *)
 	
 	(* Robot2 *)
 	(*xI_B3_M = 0 &  *)xB3_out_R2 = 0 &  (*xO_B3_R2 = 0 &  *)xB5_in_R2 = 0 &  xI_B5_R2 = 0 &  xO_B5_A = 0 & 
 	
 	(* AssemblyStation *)
 	(*xI_B5_R2 = 0 &  *)xB5_out_A = 0 &  (*xO_B5_A = 0 &  *)xproduct = 0
 	
 	(* Spec1 *)
	(*c_B1_in = 0 &  xB1_out*)

	(*------------------------------------------------------------*)
	(* Parameter constraints *)
	(*------------------------------------------------------------*)
	& p_conveyor >= 0
	& p_deadline >= 0
;


(************************************************************)
(* Property specification *)
(************************************************************)

property := unreachable loc[Spec1] = Serror;


(************************************************************)
(* The end *)
(************************************************************)
end
